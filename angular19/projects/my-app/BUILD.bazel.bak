load("@aspect_bazel_lib//lib:copy_to_bin.bzl", "copy_to_bin")
load("@aspect_rules_js//js:defs.bzl", "js_library")
load("//angular19:defs.bzl", "node_modules")
load("@npm//angular19:@angular/cli/package_json.bzl", angular_cli = "bin")
load("@npm//angular19:http-server/package_json.bzl", http_server = "bin")

js_library(
    name = "config",
    srcs = [
        "tsconfig.app.json",
        "tsconfig.spec.json",
    ],
)

TOOLS = node_modules([
    "@angular-devkit/build-angular",
])

TEST_DEPS = node_modules([
    "@angular/compiler",
    "@angular/platform-browser-dynamic",
    "@types/jasmine",
    "jasmine-core",
    "karma-chrome-launcher",
    "karma-coverage",
    "karma-jasmine",
    "karma-jasmine-html-reporter",
])

RUNTIME = node_modules([
    "@angular/core",
    "@angular/platform-browser",
    "@angular/router",
    "zone.js",
    "tslib",
])

copy_to_bin(
    name = "src",
    srcs = glob(["src/**"]),
)

angular_cli.ng_binary(name = "ng")

angular_cli.ng(
    name = "build",
    srcs = [
        ":config",
        ":src",
    ] + TOOLS + RUNTIME,
    args = ["build"],
    chdir = package_name(),
    out_dirs = ["dist"],
    tags = ["local"],  # TODO: make this work in the Bazel sandbox / remote
)

# FIXME:
# Fails due to running in the bazel run execroot which symlinks to the sources:
# [ERROR] File '../../../../src/main.ts' is missing from the TypeScript compilation. [plugin angular-compiler]
angular_cli.ng_binary(
    name = "serve",
    args = ["serve"],
    chdir = package_name(),
    data = [
        ":config",
        ":src",
    ] + TOOLS + RUNTIME,
    tags = ["local"],  # TODO: make this work in the Bazel sandbox / remote
)

http_server.http_server_binary(
    name = "serve_bundle",
    args = [package_name() + "/dist/app/browser"],
    data = [":build"],
)

angular_cli.ng_test(
    name = "test",
    args = [
        "test",
        "--watch=false",
    ],
    chdir = package_name(),
    data = [
        ":config",
        ":src",
        ":tsconfig.spec.json",
    ] + TOOLS + RUNTIME + TEST_DEPS,
    tags = ["local"],
)
