load("@bazel_env.bzl//:bazel_env.bzl", "bazel_env")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//tools:@angular/cli/package_json.bzl", angular_cli = "bin")
load("@npm//tools:svg-term-cli/package_json.bzl", svg_term_cli = "bin")

npm_link_all_packages(name = "node_modules")

svg_term_cli.svg_term_binary(name = "svg-term")

angular_cli.ng_binary(name = "ng")

# Matches keys in tools.lock.json
# See https://github.com/buildbuddy-io/bazel_env.bzl/issues/32
MULTITOOLS = [
    "ibazel",
    "buf",
    "buildozer",
    "docker-compose",
    "terraform",
    "multitool",
]

# Tools found in this package
LOCAL_TOOLS = [
    "ng",
    "svg-term",
]

# Tools found at @[toolname]
REPO_TOOLS = [
    "pnpm",
]

bazel_env(
    name = "bazel_env",
    toolchains = {
        "jdk": "@rules_java//toolchains:current_host_java_runtime",
    },
    tools = {
        "buildifier": "@buildifier_prebuilt//:buildifier",
        "go": "@rules_go//go",
        "jar": "$(JAVABASE)/bin/jar",
        "java": "$(JAVA)",
    } | {
        t: "@" + t
        for t in REPO_TOOLS
    } | {
        t: "//tools:" + t
        for t in LOCAL_TOOLS
    } | {
        t: "@multitool//tools/" + t
        for t in MULTITOOLS
    },
)
