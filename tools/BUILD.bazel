load("@bazel_env.bzl", "bazel_env")
load("@bazelrc-preset.bzl", "bazelrc_preset")
load("@multitool//:tools.bzl", MULTITOOLS = "TOOLS")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//tools:@angular/cli/package_json.bzl", angular_cli = "bin")
load("@npm//tools:svg-term-cli/package_json.bzl", svg_term_cli = "bin")

bazelrc_preset(name = "preset")

npm_link_all_packages(name = "node_modules")

svg_term_cli.svg_term_binary(name = "svg-term")

angular_cli.ng_binary(name = "ng")

filegroup(
    name = "module_includes",
    srcs = glob(["*.MODULE.bazel"]),
    tags = ["keep-sorted"],
)

# Tools found in this package
LOCAL_TOOLS = {
    t: "//tools:" + t
    for t in [
        "ng",
        "svg-term",
    ]
}

bazel_env(
    name = "bazel_env",
    toolchains = {
        "jdk": "@rules_java//toolchains:current_host_java_runtime",
        "python": "@rules_python//python:current_py_toolchain",
        "nodejs": "@nodejs_toolchains//:resolved_toolchain",
    },
    tools = {
        "buildifier": "@buildifier_prebuilt//:buildifier",
        "protoc-gen-go": "@org_golang_google_protobuf//cmd/protoc-gen-go",
        "go": "@rules_go//go",
        "jar": "$(JAVABASE)/bin/jar",
        "java": "$(JAVA)",
        "node": "$(NODE_PATH)",
        "pnpm": "@pnpm",
        "python": "$(PYTHON3)",
    } | LOCAL_TOOLS | MULTITOOLS,
)
