"Declare dependencies for bzlmod, see https://bazel.build/build/bzlmod"
module(name = "integration_testing_example")

bazel_dep(name = "aspect_bazel_lib",         version = "1.31.2")
bazel_dep(name = "aspect_rules_js",          version = "1.13.1")
bazel_dep(name = "aspect_rules_ts",          version = "1.0.5")
bazel_dep(name = "container_structure_test", version = "1.15.0")
bazel_dep(name = "gazelle",                  version = "0.28.0", repo_name = "bazel_gazelle")
bazel_dep(name = "platforms",                version = "0.0.6")
bazel_dep(name = "protobuf",                 version = "3.19.6")
bazel_dep(name = "rules_go",                 version = "0.37.0", repo_name = "io_bazel_rules_go")
bazel_dep(name = "rules_jvm_external",       version = "4.5")
bazel_dep(name = "rules_oci",                version = "1.0.0-rc0")
bazel_dep(name = "rules_pkg",                version = "0.9.1")
bazel_dep(name = "rules_python",             version = "0.21.0")

# Python
# https://github.com/bazelbuild/rules_python/tree/main/examples/bzlmod
pip = use_extension("@rules_python//python:extensions.bzl", "pip")
python = use_extension("@rules_python//python:extensions.bzl", "python")

python.toolchain(
    name = "python3",
    python_version = "3.11",
)

use_repo(python, "python3_toolchains")

register_toolchains(
    "@python3_toolchains//:all",
)

pip.parse(
    name = "pip",
    requirements_lock = "//:requirements.txt",
)

use_repo(pip, "pip")

# Java and other JVM languages:
# https://github.com/bazelbuild/rules_jvm_external/blob/master/examples/bzlmod/MODULE.bazel
# https://github.com/bazelbuild/rules_jvm_external#pinning-artifacts-and-integration-with-bazels-downloader
maven = use_extension("@rules_jvm_external//:extensions.bzl", "maven")

maven.install(
    artifacts = ["io.grpc:grpc-all:1.51.1"],
    lock_file = "//:maven_install.json",
)

use_repo(
    maven,
    "maven",
    "unpinned_maven",
)

# JavaScript and TypeScript
# https://github.com/aspect-build/rules_js/tree/main/e2e/bzlmod
# https://github.com/aspect-build/rules_ts/tree/main/e2e/bzlmod
npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm")

npm.npm_translate_lock(
    name = "npm",
    
    pnpm_lock = "//:pnpm-lock.yaml",
    npmrc = "//:.npmrc",
)

use_repo(npm, "npm")

rules_ts_ext = use_extension(
    "@aspect_rules_ts//ts:extensions.bzl",
    "ext",
    dev_dependency = True,
)

rules_ts_ext.deps()

use_repo(rules_ts_ext, "npm_typescript")

# Go
# https://github.com/bazelbuild/rules_go/tree/master/tests/bcr
go_deps = use_extension("@bazel_gazelle//:extensions.bzl", "go_deps")
go_sdk = use_extension("@io_bazel_rules_go//go:extensions.bzl", "go_sdk")

go_sdk.download(name = "go_sdk", version = "1.19.3")
use_repo(go_sdk, "go_sdk")

go_deps.module(
    path = "google.golang.org/grpc",
    version = "v1.50.1",
    sum = "h1:DS/BukOZWp8s6p4Dt/tOaJaTQyPyOoCcrjroHuCeLzY=",
    build_file_proto_mode = "disable"
)
go_deps.module(
    path = "google.golang.org/protobuf",
    sum = "h1:d0NfwRgPtno5B1Wa6L2DAG+KivqkdutMf1UhdNx175w=",
    version = "v1.28.1",
    build_file_proto_mode = "disable"
)
use_repo(go_deps, "org_golang_google_grpc", "org_golang_google_protobuf")

# Docker (OCI) containers
oci = use_extension("@rules_oci//oci:extensions.bzl", "oci")

oci.pull(
    name = "distroless_python",
    #digest = "sha256:b48e216f7c4adcf24fecd7016f3b8ead76866a19571819f67f47c1ccaf899717",
    image = "gcr.io/distroless/python3",
    tag = "latest",
)
oci.pull(
    name = "distroless_java",
    digest = "sha256:161a1d97d592b3f1919801578c3a47c8e932071168a96267698f4b669c24c76d",
    image = "gcr.io/distroless/java17",
)
use_repo(oci, "distroless_python", "distroless_java")
