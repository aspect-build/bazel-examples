"""
Given a folder with:

/myproj/genproto/example/v1/example_pb.js
/myproj/genproto/example/v1/example_pb.d.ts

Generates a js_library target in /myproj/BUILD that depends on the protobuf files and the bufbuild/protobuf library.

js_library(
    name = "protos",
    srcs = glob([
        "genproto/**/*_pb.js",
        "genproto/**/*_pb.d.ts",
    ]),
    deps = [":node_modules/@bufbuild/protobuf"],
)
"""
aspect.gazelle_rule_kind("js_library", {
    "From": "@aspect_rules_js//js:defs.bzl",
    "MergeableAttrs": ["srcs"],
    "ResolveAttrs": ["srcs"],
})

def declare_buf_js(ctx):
    if len(ctx.sources) > 0:
        ctx.targets.add(
            name = "protos",
            kind = "js_library",
            attrs = {
                "srcs": ctx.sources,
                "deps": [":node_modules/@bufbuild/protobuf"],
            },
        )

aspect.orion_extension(
    id = "buf_js",
    prepare = lambda _: aspect.PrepareResult(
        sources = [
            aspect.SourceGlobs("**/genproto/**/*_pb.js"),
            aspect.SourceGlobs("**/genproto/**/*_pb.d.ts"),
        ],
    ),
    declare = declare_buf_js,
)
