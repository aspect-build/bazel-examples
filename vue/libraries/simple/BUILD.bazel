load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//:vite/package_json.bzl", vite_bin = "bin")
load("@npm//:vue-tsc/package_json.bzl", vue_tsc_bin = "bin")

npm_link_all_packages(name = "node_modules")

_SRCS = [
    "src/InputText.vue",
    "src/InputTextarea.vue",
    "src/components.ts",
    "src/index.ts",
]

vite_bin.vite(
    name = "build",
    args = [
        "build",
        "libraries/simple",
    ],
    outs = ["dist/index.es.js"],
    srcs = _SRCS + [
        "vite.config.ts",
        "//libraries/simple:node_modules/@vitejs/plugin-vue",
    ],
)

vue_tsc_bin.vue_tsc(
    name = "types",
    args = [
        "--project",
        "libraries/simple",
    ],
    srcs = _SRCS + [
        "tsconfig.json",
        "//:node_modules/vite",
        "//:node_modules/vue",
    ],
    outs = [
        "dist/types/InputText.vue.d.ts",
        "dist/types/InputTextarea.vue.d.ts",
        "dist/types/components.d.ts",
        "dist/types/index.d.ts",
    ],
)

# TODO(alexeagle): show how this could be published to npm
npm_package(
    name = "simple",
    srcs = [
        "package.json",
        ":build",
        ":types",
    ],
    # This is a perf improvement; the default will be flipped to False in rules_js 2.0
    include_runfiles = False,
    visibility = ["//visibility:public"],
)
