load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("@aspect_rules_rollup//rollup:defs.bzl", "rollup_bundle")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")

npm_link_all_packages(name = "node_modules")

[
    rollup_bundle(
        name = format + "_bundle",
        config_file = "rollup.config.js",
        entry_point = "src/index.js",
        format = format,
        srcs = glob(["src/*"]),
        sourcemap = "false",
        deps = [
            "//libraries/simple:node_modules/rollup-plugin-vue",
            "//libraries/simple:node_modules/rollup-plugin-peer-deps-external",
        ],
    )
    for format in ["esm", "cjs"]
]

copy_file(
    name = "copy_esm",
    src = "esm_bundle",
    out = "dist/library.mjs",
)

copy_file(
    name = "copy_cjs",
    src = "cjs_bundle",
    out = "dist/library.js",
)

# TODO(alexeagle): show how this could be published to npm
npm_package(
    name = "simple",
    srcs = [
        "copy_esm",
        "copy_cjs",
        "package.json",
    ],
    package = "@yourname/yourlibrary",
    visibility = ["//visibility:public"],
)
