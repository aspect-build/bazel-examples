load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("@aspect_rules_rollup//rollup:defs.bzl", "rollup_bundle")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")

npm_link_all_packages(name = "node_modules")

[
    rollup_bundle(
        name = format + "_bundle",
        srcs = glob(["src/*"]),
        config_file = "rollup.config.js",
        entry_point = "src/index.js",
        format = format,
        sourcemap = "false",
        deps = [
            "//libraries/simple:node_modules/rollup-plugin-peer-deps-external",
            "//libraries/simple:node_modules/rollup-plugin-vue",
        ],
    )
    for format in [
        "esm",
        "cjs",
    ]
]

copy_file(
    name = "copy_esm",
    src = "esm_bundle",
    out = "dist/library.mjs",
)

copy_file(
    name = "copy_cjs",
    src = "cjs_bundle",
    out = "dist/library.js",
)

# TODO(alexeagle): show how this could be published to npm
npm_package(
    name = "simple",
    srcs = [
        "copy_cjs",
        "copy_esm",
        "package.json",
    ],
    # This is a perf improvement; the default will be flipped to False in rules_js 2.0
    include_runfiles = False,
    visibility = ["//visibility:public"],
)
