load("@aspect_rules_js//js:defs.bzl", "js_library")
load("@bazel_skylib//rules:native_binary.bzl", "native_binary")
load("@buildifier_prebuilt//:rules.bzl", "buildifier")
load("@gazelle//:def.bzl", "gazelle")
load("@npm//:defs.bzl", "npm_link_all_packages")

package(default_visibility = ["//:__subpackages__"])

# gazelle:prefix github.com/aspect-build/bazel-examples

# JS BUILD generation is opt-in per project
# gazelle:js disabled

# Global config shared across monorepo.
# gazelle:js_npm_package_target_name pkg
# gazelle:js_package_rule_kind js_library
# gazelle:js_project_naming_convention tsc

# TODO(alex): refactor this folder to follow python gazelle conventions
# gazelle:exclude py_mypy
gazelle(
    name = "gazelle",
    env = {
        "ORION_EXTENSIONS_DIR": ".aspect/cli",
        "ENABLE_LANGUAGES": ",".join([
            "buf",
            "js",
            "go",
            "proto",
            "python",
        ]),
    },
    extra_args = ["-index=lazy"],
    gazelle = "@multitool//tools/gazelle",
)

gazelle(
    name = "gazelle.check",
    env = {
        "ORION_EXTENSIONS_DIR": ".aspect/cli",
        "ENABLE_LANGUAGES": ",".join([
            "js",
            "go",
            "proto",
            "python",
        ]),
    },
    extra_args = ["-index=lazy"],
    gazelle = "@multitool//tools/gazelle",
    mode = "diff",
)

npm_link_all_packages(name = "node_modules")

exports_files(
    [
        ".clang-tidy",
        ".ruff.toml",
        ".shellcheckrc",
        "buf.yaml",
        "checkstyle.xml",
        "gazelle_python.yaml",
        "pmd.xml",
        "pyproject.toml",
    ],
)

js_library(
    name = "eslintrc",
    srcs = ["eslint.config.mjs"],
    deps = [
        ":node_modules/@eslint/js",
        ":node_modules/typescript-eslint",
    ],
)

alias(
    name = "format",
    actual = "//tools/format:format",
    visibility = ["//visibility:public"],
)

buildifier(
    name = "buildifier",
    exclude_patterns = ["./.git/*"],
    lint_mode = "fix",
    mode = "fix",
)

# todo(team): burn down to zero
DISABLED_BUILDIFIER_WARNINGS = [
    "module-docstring",
    "function-docstring",
    "unused-variable",
]

buildifier(
    name = "buildifier.check",
    exclude_patterns = ["./.git/*"],
    lint_mode = "warn",
    lint_warnings = ["-{}".format(w) for w in DISABLED_BUILDIFIER_WARNINGS],
    mode = "diff",
)
