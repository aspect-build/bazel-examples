load("@aspect_rules_js//js:defs.bzl", "js_library")
load("@buildifier_prebuilt//:rules.bzl", "buildifier")
load("@gazelle//:def.bzl", "gazelle")
load("@npm//:defs.bzl", "npm_link_all_packages")

package(default_visibility = ["//:__subpackages__"])

# Global config shared across monorepo.
# gazelle:prefix github.com/aspect-build/bazel-examples
# gazelle:js_npm_package_target_name pkg
# gazelle:js_package_rule_kind js_library
# gazelle:js_project_naming_convention tsc

# TODO(alex): refactor these folders to follow gazelle conventions
# gazelle:exclude py_mypy
# gazelle:exclude angular
# gazelle:exclude qwik

_GAZELLE_ENV = {
    "ORION_EXTENSIONS_DIR": ".aspect/cli",
    "ENABLE_LANGUAGES": ",".join([
        "buf",
        "js",
        "go",
        "proto",
        "python",
    ]),
}

gazelle(
    name = "gazelle",
    env = _GAZELLE_ENV,
    extra_args = ["-index=lazy"],
    gazelle = "@multitool//tools/gazelle",
)

gazelle(
    name = "gazelle.check",
    env = _GAZELLE_ENV,
    extra_args = ["-index=lazy"],
    gazelle = "@multitool//tools/gazelle",
    mode = "diff",
)

npm_link_all_packages(name = "node_modules")

exports_files(
    [
        ".clang-tidy",
        ".clippy.toml",
        ".ruff.toml",
        ".shellcheckrc",
        "buf.yaml",
        "checkstyle.xml",
        "gazelle_python.yaml",
        "Gemfile",
        "Gemfile.lock",
        "pmd.xml",
        "pyproject.toml",
    ],
)

js_library(
    name = "eslintrc",
    srcs = ["eslint.config.mjs"],
    deps = [
        ":node_modules/@eslint/js",
        ":node_modules/typescript-eslint",
    ],
)

alias(
    name = "format",
    actual = "//tools/format:format",
    visibility = ["//visibility:public"],
)

buildifier(
    name = "buildifier",
    exclude_patterns = ["./.git/*"],
    lint_mode = "fix",
    mode = "fix",
)

# todo(team): burn down to zero
DISABLED_BUILDIFIER_WARNINGS = [
    "module-docstring",
    "function-docstring",
    "unused-variable",
]

buildifier(
    name = "buildifier.check",
    exclude_patterns = ["./.git/*"],
    lint_mode = "warn",
    lint_warnings = ["-{}".format(w) for w in DISABLED_BUILDIFIER_WARNINGS],
    mode = "diff",
)
